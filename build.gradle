apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'sonar-runner'

version = '1.0'

task fatJar(type: Jar) {
    dependsOn project.tasks.findByName('test')
    archiveName 'TranslationHelper-all.jar'
    destinationDir new File(project.rootDir, "dist")
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest {
        attributes 'Implementation-Title': 'Translation Helper',
                'Implementation-Version': version,
                'main-class': 'de.vogel612.helper.Main'
    }
    with jar
}

task run(type: JavaExec) {
    dependsOn project.tasks.findByName('compileJava')

    classpath new File(project.rootDir, "bin"), configurations.compile
    workingDir new File(project.rootDir, "bin")
    main 'de.vogel612.helper.Main'

    args "${System.env.RUBBERDUCK_PATH}/RetailCoder.VBE/UI"
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
    compile group: 'org.jdom', name: 'jdom2', version: '2.0.6'
    // xPath dependency
    compile group: 'jaxen', name: 'jaxen', version: '1.1.6'
    testCompile group: 'junit', name: 'junit', version: '4.+'
    testCompile 'org.mockito:mockito-all:2.0.2-beta'
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}


uploadArchives {
    repositories {
        flatDir {
            dirs 'repos'
        }
    }
}

test {
    outputs.upToDateWhen { false }
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            }
        }
        exceptionFormat = 'full'
    }
}

sonarRunner {
    sonarProperties {
        property "sonar.host.url", "http://localhost:9000/"
        property "sonar.jdbc.url", "jdbc:postgresql:sonar"
        property "sonar.jdbc.driverClassName", "org.postgresql.Driver"
        property "sonar.jdbc.username", System.env.PG_SONAR_USERNAME
        property "sonar.jdbc.password", System.env.PG_SONAR_PASSWORD
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}